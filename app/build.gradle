plugins {
    id("com.android.application") version "8.5.0"
    id("org.jetbrains.kotlin.android") version "1.9.24"
}

import java.io.ByteArrayOutputStream

def runCmd(List<String> cmd) {
  def out = new ByteArrayOutputStream()
  def err = new ByteArrayOutputStream()
  def res = exec {
    commandLine cmd
    standardOutput = out
    errorOutput = err
    ignoreExitValue = true
  }
  def stdout = out.toString("UTF-8").trim()
  def stderr = err.toString("UTF-8").trim()
  if (res.exitValue != 0) {
    throw new GradleException("Command failed (${res.exitValue}): ${cmd.join(' ')}\n${stderr}\n${stdout}")
  }
  stdout
}

tasks.register("bumpVersion") {
  doLast {
    def gradleFile = file("build.gradle")
    if (!gradleFile.exists()) throw new GradleException("Missing: ${gradleFile}")

    runCmd(["git", "rev-parse", "--is-inside-work-tree"])
    def status = runCmd(["git", "status", "--porcelain"])
    if (status) throw new GradleException("Working tree not clean. Commit or stash first.\n${status}")

    def s = gradleFile.getText("UTF-8")

    def m = (s =~ /versionCode\s+(\d+)/)
    if (!m.find()) throw new GradleException("versionCode not found in ${gradleFile}")
    def oldCode = Integer.parseInt(m.group(1))
    def newCode = oldCode + 1

    s = s.replaceFirst(/versionCode\s+\d+/, "versionCode ${newCode}")

    if ((s =~ /versionName\s+"[^"]*"/).find()) {
      s = s.replaceFirst(/versionName\s+"[^"]*"/, "versionName \"${newCode}\"")
    } else if ((s =~ /versionName\s+'[^']*'/).find()) {
      s = s.replaceFirst(/versionName\s+'[^']*'/, "versionName \"${newCode}\"")
    } else {
      throw new GradleException("versionName not found in ${gradleFile}")
    }

    gradleFile.write(s, "UTF-8")

    runCmd(["git", "add", gradleFile.path])
    runCmd(["git", "commit", "-m", "chore: bump version to ${newCode}"])
    def tag = "v${newCode}"

    def existing = ""
    try { existing = runCmd(["git", "tag", "-l", tag]) } catch(Exception ignored) { }
    if (existing) throw new GradleException("Tag already exists: ${tag}")

    runCmd(["git", "tag", "-a", tag, "-m", tag])

    println("Bumped: versionCode=${newCode}, versionName=${newCode}, tag=${tag}")
  }
}

android {
    namespace "com.github.autodiag2.elm327emu"
    compileSdk 34
    ndkVersion "28.0.12433566"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    externalNativeBuild {
        ndkBuild {
            path file("autodiag/jni/Android.mk")
        }
    }

    defaultConfig {
        applicationId "com.github.autodiag2.elm327emu"
        minSdk 28
        targetSdk 34
        versionCode 12
        versionName "12"

        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
        }
        externalNativeBuild {
            ndkBuild {
                arguments(
                    "NDK_PROJECT_PATH=${projectDir}/autodiag",
                    "APP_BUILD_SCRIPT=${projectDir}/autodiag/jni/Android.mk",
                )
            }
        }
    }
    buildFeatures {
        buildConfig true
    }
    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE_PATH"))
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
        }
    }

}

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib:1.9.24")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3")
    implementation "androidx.drawerlayout:drawerlayout:1.1.1"
    implementation 'com.google.android.material:material:1.10.0'
    implementation "androidx.paging:paging-runtime:3.2.1"
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.7.0"
}

